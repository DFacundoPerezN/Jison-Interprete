imprimir nl "=== ARCHIVO DIFÃCIL ===";

procedimiento separador(cadena t = "-----") {
    imprimir nl t;
}

//  VECTORES 1D 
entero[] v = [100, 26, 1, 15, 167, 0, 76, 94, 25, 44, 5, 59, 95, 10, 23];
entero nV con valor 15;

procedimiento imprimirVector(entero n) {
    para (entero i = 0; i < n; i = i + 1) {
        imprimir nl "v[" + (cadena) i + "] = " + (cadena) v[i];
    }
}

procedimiento burbuja(entero n) {
    para (entero i = 0; i < n; i = i + 1) {
        para (entero j = 0; j < n - i - 1; j = j + 1) {
            si (v[j] > v[j + 1]) {
                entero tmp con valor v[j];
                v[j] = v[j + 1];
                v[j + 1] = tmp;
            }
        }
    }
}

procedimiento demoVector() {
    ejecutar separador("=== VECTOR (ANTES) ===");
    ejecutar imprimirVector(nV);
    ejecutar burbuja(nV);
    ejecutar separador("=== VECTOR (ORDENADO) ===");
    ejecutar imprimirVector(nV);
}

//  MATRIZ 2D (10x10) 
caracter[][] mat = [['.', '.', '.', '.', '.', '.', '.', '.', '.', '.'], ['.', '.', '.', '.', '.', '.', '.', '.', '.', '.'], ['.', '.', '.', '.', '.', '.', '.', '.', 'D', '.'], ['.', '.', '.', '.', '.', '.', '.', 'N', '.', '.'], ['.', '.', '.', '.', '.', '.', 'U', '.', '.', '.'], ['.', '.', '.', '.', '.', 'M', '.', '.', '.', '.'], ['.', '.', '.', '.', '.', '.', '.', '.', '.', '.'], ['.', '.', '.', 'A', '.', '.', '.', '.', '.', '.'], ['.', '.', 'L', '.', '.', '.', '.', '.', '.', '.'], ['.', 'O', '.', '.', '.', '.', '.', '.', '.', '.']];

entero filas con valor 10;
entero columnas con valor 10;

procedimiento imprimirMatriz() {
    para (entero i = 0; i < filas; i = i + 1) {
        cadena linea con valor "";
        para (entero j = 0; j < columnas; j = j + 1) {
            linea = linea + (cadena) mat[i][j];
        }
        imprimir nl linea;
    }
}

procedimiento voltearFilas() {
    // invertir verticalmente
    para (entero x = 0; x < filas / 2; x = x + 1) {
        entero y con valor filas - 1 - x;
        para (entero j = 0; j < columnas; j = j + 1) {
            caracter t con valor mat[x][j];
            mat[x][j] = mat[y][j];
            mat[y][j] = t;
        }
    }
}

procedimiento demoMatriz() {
    ejecutar separador("=== MATRIZ (ORIGINAL) ===");
    ejecutar imprimirMatriz();
    ejecutar voltearFilas();
    ejecutar separador("=== MATRIZ (VOLTEADA) ===");
    ejecutar imprimirMatriz();
}

// OBJETOS 
objeto Persona ( nombre cadena
    edad entero)

Persona con metodo setNombre(cadena n) { 
    nombre = n; 
}

Persona con metodo setEdad(entero e) { 
    edad = e; 
}

Persona con metodo cumplir() {
    edad = edad + 1;
}

Persona con metodo saludar() {
    imprimir nl "Hola, soy " + nombre + " y tengo " + (cadena) edad;
}

procedimiento demoObjetos() {
    ingresar objeto Persona p -> Persona("Ana", 30);
    ejecutar p.saludar();
    ejecutar p.cumplir();
    ejecutar p.saludar();
}

// CASTEOS
procedimiento demoCasteos() {
    entero e con valor 65;
    decimal dd con valor 12.75;
    caracter ch con valor 'B';

    cadena se con valor (cadena) e;
    imprimir nl "Entero a cadena: " + se;

    caracter ce con valor (caracter) 65;
    imprimir nl "Entero a caracter: " + ce;

    entero ei con valor (entero) ch;
    imprimir nl "Caracter a entero: " + (cadena) ei;

    decimal ed con valor (decimal) ch;
    imprimir nl "Caracter a decimal: " + (cadena) ed;

    decimal de con valor (decimal) e;  // 65.0
    entero ie con valor (entero) dd;   // 12
    imprimir nl "Entero->Decimal: " + (cadena) de + " | Decimal->Entero: " + (cadena) ie;

    cadena sd con valor (cadena) dd;
    imprimir nl "Decimal a cadena: " + sd;
}

//  RECURSIVAS
funcion entero fib(entero n) {
    si (n <= 1) { retornar n; }
    retornar fib(n - 1) + fib(n - 2);
}

procedimiento hanoi(entero n, cadena desde, cadena hacia, cadena aux) {
    si (n == 0) { retornar; }
    ejecutar hanoi(n - 1, desde, aux, hacia);
    imprimir nl "Mover disco " + (cadena) n + " de " + desde + " a " + hacia;
    ejecutar hanoi(n - 1, aux, hacia, desde);
}

funcion entero ack(entero m, entero n) {
    si (m == 0) { retornar n + 1; }
    si (n == 0) { retornar ack(m - 1, 1); }
    retornar ack(m - 1, ack(m, n - 1));
}

procedimiento demoRecursivas() {
    ejecutar separador("=== FIBONACCI ===");
    entero f5 con valor fib(5);
    imprimir nl "fib(5) = " + (cadena) f5;

    ejecutar separador("=== HANOI (3 discos) ===");
    ejecutar hanoi(3, "A", "C", "B");

    ejecutar separador("=== ACKERMANN (2,1) ===");
    entero a21 con valor ack(2, 1);
    imprimir nl "ack(2,1) = " + (cadena) a21;
}

//  MAIN 
procedimiento main() {
    ejecutar demoVector();
    ejecutar demoMatriz();
    ejecutar demoObjetos();
    ejecutar demoCasteos();
    ejecutar demoRecursivas();
    ejecutar separador("=== FIN ARCHIVO DIFÃCIL ===");
}

ejecutar main();
